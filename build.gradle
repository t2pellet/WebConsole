plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url "https://maven.shedaniel.me/" }
}

configurations {
    shade
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:6.1.48") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    shade 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.nanohttpd:nanohttpd-websocket:2.3.1'
    shade 'org.nanohttpd:nanohttpd-websocket:2.3.1'
    implementation 'net.lingala.zip4j:zip4j:2.9.1'
    shade 'net.lingala.zip4j:zip4j:2.9.1'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}


java {
    withSourcesJar()
}

task zipServer(type: Zip) {
    archiveFileName = 'client.zip'
    destinationDirectory = file('src/main/resources/')

    from layout.projectDirectory.dir('client')
}

processResources {
    dependsOn(zipServer)
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

shadowJar {
    configurations = [project.configurations.shade]
}

remapJar {
    dependsOn(shadowJar)
    (input as FileSystemLocationProperty<? extends FileSystemLocation>).set(shadowJar.archivePath);
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
    }
}